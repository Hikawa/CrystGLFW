module CrystGLFW
  module Event
    # A WindowMouseButtonEvent is generated by the `Window#on_mouse_button` callback and contains a `MouseButton` along with
    # an interface to determine what action occurred with the key.
    struct WindowMouseButton < Any
      include Modifiers

      @@actions : Array(Symbol) = [:press, :release]
      @action : Symbol | Nil

      getter window : CrystGLFW::Window
      getter mouse_button : MouseButton

      # :nodoc:
      def initialize(window : Window, mouse_button : MouseButton, action_code : Int32, modifiers : Int32)
        @window = window
        @mouse_button = mouse_button
        @action = @@actions.find { |action| CrystGLFW[action] == action_code }
        set_modifiers modifiers
      end

      # Returns true if the mouse button was pressed. False otherwise.
      #
      # ```
      # window.on_mouse_button do |event|
      #   puts "a mouse button was pressed." if event.press?
      # end
      # ```
      def press?
        @action == :press
      end

      # Returns true if the mouse button was released. False otherwise.
      #
      # ```
      # window.on_mouse_button do |event|
      #   puts "a mouse button was released." if event.release?
      # end
      # ```
      def release?
        @action == :release
      end
    end
  end
end
