module CrystGLFW
  module Event
    # A WindowKeyEvent is generated by the `Window#on_key` callback and contains a `Key` along with
    # an interface to determine what action occurred with the key.
    struct WindowKey < Any
      include Modifiers

      @action_code : Int32

      getter window : CrystGLFW::Window
      getter key : CrystGLFW::Key

      # :nodoc:
      def initialize(window : Window, key : Key, action_code : Int32, modifiers : Int32)
        @window = window
        @key = key
        @action_code = action_code
        set_modifiers modifiers
      end

      # Returns true if the key was pressed. False otherwise.
      #
      # ```
      # window.on_key do |key_event|
      #   puts "a key was pressed." if key_event.press?
      # end
      # ```
      def press?
        @action_code == CrystGLFW[:press]
      end

      # Returns true if the key was released. False otherwise.
      #
      # ```
      # window.on_key do |key_event|
      #   puts "a key was released." if key_event.release?
      # end
      # ```
      def release?
        @action_code == CrystGLFW[:release]
      end

      # Returns true if the key was held down. False otherwise.
      #
      # ```
      # window.on_key do |key_event|
      #   puts "a key was held down." if key_event.repeat?
      # end
      # ```
      def repeat?
        @action_code == CrystGLFW[:repeat]
      end
    end
  end
end
